---
subcategory: "Identity and Access Management (IAM)"
page_title: "HSDP: hsdp_iam_service"
description: |-
  Manages HSDP IAM Service resources
---

# hsdp_iam_service

Provides a resource for managing HSDP IAM services of an application under a proposition.

## Example Usage

The following example creates a service

```hcl
resource "hsdp_iam_service" "testservice" {
  name                = "TESTSERVICE"
  description         = "Test service"
  application_id      = var.app_id

  validity            = 12   # Months
  
  token_validity      = 3600 # Seconds

  scopes              = ["openid"]
  default_scopes      = ["openid"]
}
```

The followin example creates a service with an external managed certificate

```hcl
resource "tls_private_key" "testservice" {
  algorithm = "RSA"
  rsa_bits  = 4096
}

resource "tls_self_signed_cert" "testservice" {
  private_key_pem = tls_private_key.testservice.private_key_pem

  subject {
    common_name  = "testservice." # Should match `service.name` + "." (dot)
  }

  validity_period_hours = 24

  allowed_uses = [  ]
}

resource "hsdp_iam_service" "testservice" {
  name                = "testservice"
  description         = "Test service"
  application_id      = var.app_id

  validity            = 12   # Months
  
  token_validity      = 3600 # Seconds

  scopes              = ["openid"]
  default_scopes      = ["openid"]
  
  self_managed_certificate = tls_self_signed_cert.testservice.cert_pem
}
```

## Argument Reference

The following arguments are supported:

* `name` - (Required) The name of the service
* `description` - (Required) The description of the service
* `application_id` - (Required) the application ID (GUID) to attach this service to
* `scopes` - (Required) Array. List of supported scopes for this service. Minimum: ["openid"]
* `validity` - (Optional) Integer. Validity of service (in months). Minimum: 1, Maximum: 600 (5 years), Default: 12
* `token_validity` - (Optional) Integer. Access Token Lifetime (in seconds). Default: 1800 (30 minutes), Maximum: 2592000 (30 days)
* `default_scopes` - (Required) Array. Default scopes. You do not have to specify these explicitly when requesting a token. Minimum: ["openid"]
* `self_managed_private_key` - (Deprecated, Optional)  RSA private key in PEM format. When provided, overrides the generated certificate / private key combination of the
  IAM service. This gives you full control over the credentials. When not specified, a private key will be generated by IAM
* `self_managed_expires_on` - (Deprecated, Optional) Sets the certificate validity. When not specified, the certificate will have a validity of 5 years.
* `self_managed_certificate` - (Optional) X509 Certificate in PEM format. When provided, overrides the generated certificate / private key combination of the IAM service.
  This gives you full control over the credentials. When not specified, a private key will be generated by IAM. Mutually exclusive with `self_managed_private_key`
* `self_managed_certificate_nonsensitive` - (Optional) X509 Certificate in PEM format. When provided, overrides the generated certificate / private key combination of the IAM service.
  This gives you full control over the credentials. When not specified, a private key will be generated by IAM. Mutually exclusive with `self_managed_private_key`

## Attributes Reference

In addition to all arguments above, the following attributes are exported:

* `id` - The GUID of the client
* `service_id` - (Generated) The service id
* `private_key` - (Generated) The active private of the service
* `expires_on` - (Generated) Sets the certificate validity. When not specified, the certificate will have a validity of 5 years.
* `organization_id` - The organization ID this service belongs to (via application and proposition)

## Import

Existing services can be imported, however they will be missing their private key rendering them pretty much useless. Therefore, we recommend creating them using the provider.
