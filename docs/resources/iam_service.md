# hsdp_iam_service

Provides a resource for managing HSDP IAM services of an application under a proposition.

## Example Usage

The following example creates a service

```hcl
resource "hsdp_iam_service" "testservice" {
  name                = "TESTSERVICE"
  description         = "Test service"
  application_id      = var.app_id

  validity            = 12

  scopes              = ["openid"]
  default_scopes      = ["openid"]
}
```

## Self-managed certificates

This resource allows you to set up self-managed certificates which gives you full
control over the validity. It also allows you to auto-rotate the certificate. Below is 
an example script that auto-rotates the service identity certificate every 30 day 
(Note: schedule a Terraform run at least once a day, so the change is triggered)

```hcl
resource "tls_private_key" "mykey" {
  algorithm = "RSA"
  rsa_bits  = 4096
}

resource "tls_self_signed_cert" "cert" {
  key_algorithm   = "RSA"
  private_key_pem = tls_private_key.mykey.private_key_pem

  subject {
    common_name  = "testservice.demo.demoproposition@clientteamorg.philips-healthsuite.com"
  }

  validity_period_hours = 720
  early_renewal_hours   = 48
  is_ca_certificate     = true
  
  allowed_uses = [
    "server_auth",
  ]
}

resource "hsdp_iam_service" "test" {
  name                     = "TestService"
  description              = "TestDesc"
  application_id           = "bfa887bb-c7e8-42bb-bee4-e1ce83f34643"
  scopes                   = ["openid"]
  default_scopes           = ["openid"]
  #self_managed_private_key = tls_private_key.test.private_key_pem
  #expires_on               = "2022-10-01T07:20:50.52Z"
  self_managed_certificate = tls_self_signed_cert.cert.cert_pem
}
```

## Argument Reference

The following arguments are supported:

* `name` - (Required) The name of the service
* `description` - (Required) The description of the service
* `application_id` - (Required) the application ID (GUID) to attach this service to
* `scopes` - (Required) Array. List of supported scopes for this service. Minimum: ["openid"]
* `validity` - (Optional) Integer. Validity of service (in months). Minimum: 1, Maximum: 600, Default: 12
* `default_scopes` - (Required) Array. Default scopes. You do not have to specify these explicitly when requesting a token. Minimum: ["openid"]
* `self_managed_private_key` - (Optional)  RSA private key in PEM format. When provided, overrides the generated certificate / private key combination of the
  IAM service. This gives you full control over the credentials. When not specified, a private key will be generated by IAM.
  Conflicts with `self_managed_certificate`
* `expires_on` - (Optional) Sets the certificate validity. When not specified, the certificate will have a validity of 5 years.
* `self_managed_certificate` - (Optional) Self signed certificate in PEM. When provided, overrides the generated certificate / private key generated by IAM.
  The certificate must conform to the what IAM expects. Conflicts with `self_managed_private_key`

## Attributes Reference

The following attributes are exported:

* `id` - The GUID of the client
* `service_id` - (Generated) The service id
* `private_key` - (Generated) The active private of the service
* `organization_id` - The organization ID this service belongs to (via application and proposition)

## Import

Existing services can be imported, however they will be missing their private key rendering them pretty much useless. Therefore, we recommend creating them using the provider.
